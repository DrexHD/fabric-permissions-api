plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
    id 'net.kyori.indra.publishing.sonatype' version '3.1.3'
    id 'net.kyori.indra.publishing' version '3.1.3'
}

repositories {
    maven { url 'https://maven.fabricmc.net/' }
}

def minecraftVersion = '1.20.1'
def yarnBuild = 4
def loaderVersion = '0.14.21'
def fabricApiVersion = '0.86.0+1.20.1'

group = 'me.lucko'
version = '0.3-SNAPSHOT'
description = 'A simple permissions API for Fabric'

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${minecraftVersion}+build.${yarnBuild}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loaderVersion}"
    modImplementation(fabricApi.module("fabric-api-base", fabricApiVersion))
}

processResources {
    inputs.property "version", project.version

    filesMatching('fabric.mod.json') {
        expand "version": project.version
    }

    filteringCharset = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 8
}

indra {
    github("lucko", "fabric-permissions-api") {
        ci(true)
    }
    mitLicense()
    configurePublications {
        pom {
            developers {
                developer {
                    id = 'Luck'
                    url = 'https://lucko.me'
                }
            }
        }
    }
    signWithKeyFromProperties("signingKey", "signingPassword")
}
